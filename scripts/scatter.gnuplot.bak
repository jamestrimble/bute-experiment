# vim: set et ft=gnuplot sw=4 :

set terminal tikz standalone color size 9cm,8cm font '\scriptsize' preamble '\usepackage{times,microtype,algorithm2e,algpseudocode,amssymb}'
set output "plots/scatter.tex"

set key off
set xlabel 'Our algorithm'
set ylabel 'SAT encoding'
set border 3
set grid x y front lc rgb "#999999" lw 1 lt 0
set xtics nomirror
set ytics nomirror
set tics front
set size square
set xrange [1:1e6]
set yrange [1:1e6]
#set x2range [-0.5:50.5]
#set y2range [-0.5:50.5]
set logscale x
set logscale y
set format x '$10^{%T}$'
set format y '$10^{%T}$'

set label 1 "Famous" at 20000,32 point pt 5 ps 1.2 lc 1 offset 0,0 left
set label 2 "Standard" at 20000,32 point pt 7 ps 1.2 lc 2 offset 0,-1 left
set label 3 "Random" at 20000,32 point pt 9 ps 1.2 lc 3 offset 0,-2 left
set label 4 "PACE" at 20000,32 point pt 9 ps 1.2 lc 3 offset 0,-3 left

plot \
  x w l lc rgb '#999999' lw 1 lt 0, \
  "results/results-for-plot.txt" u ($5*1000):($6*1000):($1*2+3):1 w points notitle pt var ps .8 lc var

